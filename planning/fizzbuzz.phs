"stdlib/prelude.phs" include use
"stdlib/str.phs" include use
"stdlib/io.phs" include use

# Define print for numbers
{ { $io  $v } =
    v type I32 I64 | F32 F64 | | ==
    io type Any IO == && }
{ { $io  $v } =
    io v { io type v type } { io type } Arrow { "js" "console.log" } import @
} $print fun

# Define print for strings
{ { $io  $str } =
    str type Str ==
    io type Any IO == && }
{ { $io  $str } =
    io str { io type Str } { io type } Arrow
        { "js" "logStr" } import @
} $print fun

# export for performing IO
{ Void IO  I32 } {
    $n =

    # Some strings
    "fizz" Str as $fizz =
    "buzz" Str as $buzz =
    "fizzbuzz" Str as $fizzbuzz =

    # fizzbuzz logic
    { true } { print } $fb fun
    { 3 % 0 == } { pop fizz print } $fb fun
    { 5 % 0 == } { pop buzz print } $fb fun
    { $n =  n 3 % 0 ==  n 5 % 0 == && }
        { pop fizzbuzz print } $fb fun

    # from 1 - n do fizzbuzz
    {
        { $n $i } =
        { true } { @ } $cond fun
        { i n <= } {
            i fb
            n  i 1 +  iter
        } $cond fun
        cond
    } rec $iter =
    n 1 iter
} $main export
