"./prelude.phs" include use

# Average
{ + 2.0 / } $avg =

# Absolute value
{ true } {} $abs fun
{ 0.0 < } { -1.0 * } $abs fun

{ # Square route via newton's method
	{ # Tail-recursive helper
        $guess =
        $x =

        # Do we have an acceptable margin of error?
        { guess guess * x - abs x / 1.0e-15 < } $good_enough =

        # We can improve the value by averaging with quotient
        { x x guess / guess avg iter } $improve =

        # Improve value until it's good enough
        { true } { improve } $cond fun
        { good_enough } { guess } $cond fun
        cond
	} rec $iter =

    # Start calculating with first guess being 1
    1.0 iter
} $sqrt =


# Factorial
{ true } {
    {
        { $ret $n } =
        { true } { n } $branch fun
        { n 0 > } { ret n * n 1 - iter } $branch fun
        branch
    } rec iter
} $fac fun

# Use bigger of two values
{ true } { pop } $max fun
{ < } { { $a $b } = b } $max fun

# Use smaller of two values
{ true } { pop } $min fun
{ > } { { $a $b } = b } $min fun

